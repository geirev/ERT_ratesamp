program sample
! Samples the measurement perturbation matrix E for the ert-Eclipse applications.
! The program reads the ert/observations/obshist.txt file and the schedulefile defined below.
! Only configured for opr, wpr, gpr observations.
! Predefined well names.

   use m_pseudo1D
   use m_fixsample1D
   use m_tecfld
   use m_set_random_seed2
   use m_random
   implicit none

   integer, parameter :: nrens=100        ! ensemble size
! read observations info assumed stored as follows:
   integer, parameter :: nx=36            ! Number of dates keywords
   integer, parameter :: nrwells=5        ! number of wells
   integer, parameter :: nrdata=3         ! number of variables per well
! We will then sample nrwells*nrdata time series of length nx and store these as one realization
! A total of nrens such realizations are computed.

   real :: xlength=35.0                   ! length of time series is 36 months
   real :: cor1=15.0                      ! decorrelation length in months
   character(len=100) :: obshistfile='/home/geve/Dropbox/Statoil/erterr/observations/obshistnew.txt'
   character(len=100) :: schedulefile='/home/geve/Dropbox/Statoil/eclipse/include/history.sch'
   character(len=100) :: outpath='/home/geve/Dropbox/Statoil/erterr/'
   character(len=100) :: controlpath='/home/geve/Dropbox/Statoil/eclipse/Priors/control/'
   integer n1                             ! x-dimension of grid
   integer i,j,k,l,m,ic,j1
   integer idat
   integer iwell
   integer ipos(4)
   real dx

   real B(nx,nrwells*nrdata*nrens)                       !  1D samples
   real A(nx*nrwells*nrdata,nrens)
   real C(nx, nrdata, nrwells, nrens)
   real obs(nx,nrdata,nrwells)
   real stddev(nx,nrdata,nrwells)

   type wellrates
      character(len=12) date
      character(len=4)  wellname(nrwells)
      real opr(nrwells)
      real gpr(nrwells)
      real wpr(nrwells)
   end type
   type (wellrates) rates(nx)

   character(len=100) cline
   character(len=4) var
   character(len=4) well
   character(len=4) cens
   real rel_err
   real min_err

   integer istat
   integer noise
   character(len=200) string1
   character(len=200) string2
   string1="grep -A1 -e 'DATES' -e 'OPEN RESV' "//trim(schedulefile)// " > rates.txt"
   string2="sed -i -e '/^\/$/d' -e '/--/d' -e 's?/??' -e 's/ OPEN RESV //' -e 's/ /#/g'  rates.txt"

   write (*,'(a)',advance='no')'Select type of noise (0-null, 1-pseudo, 2-white, 3-constant): '
   read(*,*)noise

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Set a variable random seed
   call set_random_seed2

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Correction for Gaussian variogram
   cor1=cor1/sqrt(3.0)

   dx=xlength/float(nx-1)
   n1=nint(real(nx)*1.5)

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Sample all random time series
   select case (noise)
   case (0)
      B=0.0
      print *,'Simulating noise=0.0'
   case (1)
      call pseudo1D(B,nx,nrwells*nrdata*nrens,cor1,dx,n1)
      call fixsample1D(B,nx*nrwells*nrdata,nrens)
      print *,'Simulating pseudo random red noise'

   case (2)
      print *,'Simulating white random noise'
      call random(B,nx*nrwells*nrdata*nrens)

   case (3)
      print *,'Simulating random bias noise'
      B=-1.0
      call random(B(1,1:nrwells*nrdata*nrens),nrwells*nrdata*nrens)
      do i=1,nrwells*nrdata*nrens
         B(2:nx,i)=B(1,i)
      enddo
   end select

! test
   do i=1,nx
      write(*,'(i4,12f10.2)')i,B(i,1:12)
   enddo

! testplot
   A=reshape(B,(/ nx*nrwells*nrdata, nrens /) )
   open(10,file='tec_ensA.dat')
      do i=1,nx*nrwells*nrdata
         write(10,'(i3,f12.2,100f12.2)')i,(i-1)*dx,A(i,1:min(nrens,10))
      enddo
   close(10)

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Extract rates from schedule file amd dump them in rates.txt
   print '(a,a)','string1=',trim(string1)
   istat = system( string1 )
   print '(a,a)','string2=',trim(string2)
   istat = system( string2 )
   istat = system( 'head rates.txt' )

! initializing rates to 0 and setting well names
   do i=1,nx
      do j=1,nrwells
         write(rates(i)%wellname(j),'(a,i1)')'OP_',j
         rates(i)%opr(j)=0.0
         rates(i)%wpr(j)=0.0
         rates(i)%gpr(j)=0.0
         rates(i)%date='xxxxxxxxxxxx'
      enddo
   enddo

! reading rates and times from rates.txt
   open (10,file='rates.txt')
   idat=1
   do i=1,10000
      read(10,'(a)',end=200)cline
      if (cline(1:1) == 'D') then
         read(10,'(a)',end=200)cline
         rates(idat)%date=cline(3:15)
         idat=idat+1
      else
         read(cline(6:6),*)iwell
         ic=0
         do j=3,len_trim(cline)
            if(cline(j:j)=='#') then
               ic=ic+1
               ipos(ic)=j
            endif
         enddo
         read(cline(ipos(1)+1:ipos(2)-1),*)rates(idat)%opr(iwell)
         read(cline(ipos(2)+1:ipos(3)-1),*)rates(idat)%wpr(iwell)
         read(cline(ipos(3)+1:ipos(4)-1),*)rates(idat)%gpr(iwell)
      endif
   enddo
   200 close(10)

! check file of all rate measurements
   open(10,file='output.dat')
      do i=1,nx
         do j=1,nrwells
            write(10,'(a12,tr1,a4,tr2,2f12.3,e15.3)')&
              rates(i)%date,rates(i)%wellname(j),rates(i)%opr(j),rates(i)%wpr(j),rates(i)%gpr(j)
         enddo
      enddo
   close(10)

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Scaling perturbations according to measurement magnitude and error defs in obshistfile
   do k=1,nx
      do i=1,nrwells
         obs(k,1,i) = rates(k)%opr(i)
         obs(k,2,i) = rates(k)%wpr(i)
         obs(k,3,i) = rates(k)%gpr(i)
      enddo
   enddo

   open(10,file=trim(obshistfile),err=100)
   do m=1,100000
      read(10,'(tr21,a3,tr1,a4,tr11,f4.2,tr36,f6.1)',end=100) var,well,rel_err,min_err
      write(*,'(a,a3,a,a4,a,f5.3,a,f8.3)') ' var=',var,' well=',well,' rel_err=',rel_err,' min_err=',min_err
      select case (var)
      case('OPR')
         l=1
      case('WPR')
         l=2
      case('GPR')
         l=3
      end select
      read(well(4:4),'(i1)')i

      do k=1,nx
         if (obs(k,l,i) /= 0.0) then
            stddev(k,l,i)=max(abs(rel_err*obs(k,l,i)),min_err)
         else
            stddev(k,l,i)=0.0
         endif
      enddo
   enddo
   100 close(10)

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Saving perturbations for schedule parcing
   select case (noise)
   case (0)
      open(10,file=trim(outpath)//'EPERT0_0')
   case (1)
      open(10,file=trim(outpath)//'EPERT_0')
   case (2)
      open(10,file=trim(outpath)//'EPERTW_0')
   case (3)
      open(10,file=trim(outpath)//'EPERTB_0')
   end select
   C=reshape(B,(/ nx, nrdata, nrwells, nrens /) )
   write(10,'(4(a,i6))')'nrens=',nrens,' nrwells=',nrwells,' nrdata=',nrdata,' nx=',nx
   do j=1,nrens
      cens(:)='    '
      j1=j-1
      if (j1<10)                 write(cens(1:1),'(i1.1)')j1
      if (j1>=10 .and. j1<100)   write(cens(1:2),'(i2.2)')j1
      if (j1>=100 .and. j1<1000) write(cens(1:3),'(i3.3)')j1
      open(11,file=trim(controlpath)//'CONTROL_'//trim(cens)) 
      do i=1,nrwells
         do l=1,nrdata
            do k=1,nx
               C(k,l,i,j)=stddev(k,l,i)*C(k,l,i,j)
               write(10,'(4i4,e15.6)')k,l,i,j,C(k,l,i,j)
               write(11,'(e13.5)')C(k,l,i,j)
            enddo
         enddo
      enddo
      close(11)
   enddo
   close(10)

   open(12,file=trim(controlpath)//'control.txt')
   do i=1,nrwells
      write(12,'(a,a,i4)')rates(1)%wellname(i),':OPR:',nx
      write(12,'(a,a,i4)')rates(1)%wellname(i),':WPR:',nx
      write(12,'(a,a,i4)')rates(1)%wellname(i),':GPR:',nx
      write(12,*)
   enddo
   close(12)

   open(12,file=trim(controlpath)//'wells.txt')
   do i=1,nrwells
      write(12,'(a)')rates(1)%wellname(i)
   enddo
   close(12)


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Saving perturbations for analysis routine
!   open(10,file='E.dat')
!      do j=1,nrens
!      do i=1,nx*nrwells*nrdata
!         write(10,'(i6,i4,e15.6)')i,j,A(i,j)
!      enddo
!      enddo
!   close(10)

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! testplot
! testplot
   A=reshape(C,(/ nx*nrwells*nrdata, nrens /) )
   open(10,file='tec_ensB.dat')
      do i=1,nx*nrwells*nrdata
         write(10,'(i3,f12.2,100f12.2)')i,(i-1)*dx,A(i,1:min(nrens,10))
      enddo
   close(10)


end program
